subprojects {

    group = 'com.github.mostroverkhov.r2'
    version = mavenversion

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
        maven { url 'https://oss.jfrog.org/libs-snapshot' }
    }

    ext {
        kotlin_version = '1.2.31'
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled true
        }
    }

    check.dependsOn jacocoTestReport


    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar, jar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar {
                    classifier "sources"
                }

                artifact javadocJar {
                    classifier "javadoc"
                }
            }
        }
    }
}
