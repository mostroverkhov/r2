buildscript {
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
    }
}

apply plugin: 'com.google.protobuf'

ext {
    protoc_version = '3.5.0'
    protoc_lite_version = '3.0.0'
}

dependencies {
    api project(":core")
    api "com.google.protobuf:protobuf-lite:$protoc_lite_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7"
}

sourceSets {
    test {
        java {
            srcDirs "src/test/generated"
        }
    }
}

protobuf {

    protoc {
        artifact = "com.google.protobuf:protoc:$protoc_version"
    }

    plugins {
        javalite {
            // The codegen for lite comes as a separate artifact
            artifact = "com.google.protobuf:protoc-gen-javalite:$protoc_lite_version"
        }
    }

    generatedFilesBaseDir = "$projectDir/src/test/generated"

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                // In most cases you don't need the full Java output
                // if you use the lite output.
                remove java
            }
            task.plugins {
                javalite {
                    outputSubDir = ''
                }
            }
        }
    }
}